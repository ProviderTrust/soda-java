import org.ajoberstar.grgit.Grgit

sourceSets {
    main {
        resources {
            srcDirs += 'src/main/build'
        }
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:all,-deprecation,-processing'
    options.compilerArgs << '-parameters'
    options.incremental = true
}
compileTestJava { options.incremental = true }


configurations {
    runtimeaspects { transitive = false }
    compiletimeaspects { transitive = false }
}
dependencies {
    compile(["com.opencsv:opencsv:3.10",
    "joda-time:joda-time:2.1",
    "com.sun.jersey:jersey-bundle:1.9.1",
    "com.sun.jersey.contribs:jersey-multipart:1.9.1",
    "com.fasterxml.jackson.core:jackson-core:2.8.6",
    "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.8.6",
    "com.fasterxml.jackson.datatype:jackson-datatype-joda:2.8.6",
    "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.8.6",
    "com.google.guava:guava:22.0",
    "org.apache.commons:commons-lang3:3.6",
    "com.google.code.findbugs:annotations:2.0.1",
    "commons-beanutils:commons-beanutils:1.8.3",
    "commons-io:commons-io:1.3.2"])
    testCompile "com.novocode:junit-interface:0.9"
}

task createDependencyFile() {
    group = 'Build'
    doFirst {
        def dependencyFile = file("dependencies.txt")
        def pw = dependencyFile.newPrintWriter()
        configurations.all.collect {it.resolvedConfiguration.getResolvedArtifacts()}.flatten().collect {it.moduleVersion.id}.
            unique().sort(false){"${it.group}:${it.name}"}forEach{
                pw.println(it)
            }
        pw.close()
        try{
            def root = file('../.git').exists() ? file('..') : file('.')
            def grgit = Grgit.open(root)
            def path = root == project.rootDir ? dependencyFile.name : "${project.rootDir.name}/${dependencyFile.name}"
            if(isIDEA)
                grgit.add(patterns: [path] as Set, update: false)
            else
                grgit.commit(message: 'Updating dependencies.txt', paths: [path] as Set)

        }catch(ignore){
            if(!ignore.message.toLowerCase().contains("no changes"))
                ignore.printStackTrace()
        }
    }
}
if(isIDEA) {
    afterEvaluate {
        createDependencyFile.execute()
    }
}